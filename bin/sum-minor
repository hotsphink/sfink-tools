#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;

my @modes;
my $action = 'median';
my $whichrun;
GetOptions("median!" => sub { $action = 'median'  },
           "mean!" => sub { $action = 'mean'  },
           "--mode=s" => \@modes,
           "--run=s" => \$whichrun,
    );

# Default: look for runs where nursery was either on or off.
@modes = qw(on off) if @modes == 0;

my %data; # { "mode-variant" => [run: { field name => [value] } ] }
my @FIELDNAMES; # [ field name, raw or "nursery.whatever" ]
my %nonnumeric;
my %ALLFIELDS; # Set of all field names

my @order;
my %seen;

my %found_mode;

my @variants = @ARGV ? @ARGV : qw(inbound jit);

if (-d "results") {
    my $dir;
    if (defined $whichrun) {
        ($dir) = grep { -d $_ } ($whichrun, "results/$whichrun", "results/run$whichrun");
    } else {
        my ($latest) = qx(ls -t results);
        chomp($latest);
        $dir = "results/$latest";
        print "Reporting on $dir\n";
    }
    chdir($dir) or die "cd $dir: $!";
}

for my $variant (@variants) {
    MODE: for my $mode (@modes) {
        for my $n (1..5) {
            my $logfile = "$variant.$mode.$n.log";
            next MODE if ! -e $logfile;
            $found_mode{$mode} = 1;
            open(FILE, "<", $logfile) or die "open $logfile: $!";
            while(<FILE>) {
                if (/^MinorGC:/) {
                    my @F = split;
                    if (!@FIELDNAMES) {
                        @FIELDNAMES = map { "nursery.$_" } @F;
                    }
                    next if $F[1] eq 'Reason';
                    for my $i (0 .. $#F) {
                        my $field = $FIELDNAMES[$i];
                        push @{ $data{"$mode-$variant"}[$n]{$field} }, $F[$i];
                        $nonnumeric{$field} = 1 if $F[$i] !~ /^\d+$/;
                        $ALLFIELDS{$field} = 1;
                        push @order, $field if ! $seen{$field}++ && !$nonnumeric{$field};
                    }
                }
            }
            
            $logfile = "$variant.$mode.$n.txt";
            open(FILE, "<", $logfile) or die "open $logfile: $!";
            while(<FILE>) {
                if (/^(\w+): (\d+)/) {
                    $data{"$mode-$variant"}[$n]{$1} = [ $2 ];
                    $ALLFIELDS{$1} = 1;
                    push @order, $1 if ! $seen{$1}++;
                }
            }
        }
    }
}

sub compute_average {
    my ($rundata, $field) = @_;
    my @sums;
    for my $n (1..5) {
        my $sum = 0;
        $sum += $_ foreach @{ $rundata->[$n]{$field} };
        push @sums, $sum;
    }

    if ($action eq 'median') {
        return (sort { $a <=> $b } @sums)[int(@sums / 2)];
    } elsif ($action eq 'mean') {
        my $sum = 0;
        $sum += $_ foreach @sums;
        return $sum / @sums;
    } else {
        return 'fnord';
    }
}

if (keys %found_mode == 0) {
    die "No results found for any mode!\n";
}

for my $mode (sort keys %found_mode) {
    print "$action with nursery strings $mode of ", join(" -> ", @variants), "\n";
    for my $field (@order) {
        next if $field eq 'nursery.Size'; # This is a sum of nursery sizes across the run. Not helpful.
        my $base = compute_average($data{"$mode-$variants[0]"}, $field);
        my $base_total_time = compute_average($data{"$mode-$variants[0]"}, 'nursery.total');
        my $istime = $field =~ /^nursery\./;
        next if $istime && $base < 500;
        next if $istime && $base < 0.01 * $base_total_time;
        (my $pfield = $field) =~ s/nursery./(nursery) /;
        printf "% 20s: ", $pfield;
        for my $i (0 .. $#variants) {
            print " -> " if $i;
            my $score = compute_average($data{"$mode-$variants[$i]"}, $field);
            printf("% 8d", $score);
            if ($i) {
                my $delta = $score - $base;
                printf " % +6d (%+ 5.1f%%) (%+ 6.2f%%)", $delta, $delta / $base * 100, $delta / $base_total_time * 100;
                print(($istime xor $delta > 0) ? " improvement" : " regression");
            }
        }
        print "\n";
    }
}
